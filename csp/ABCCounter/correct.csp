include "common.csp"


instance VarA = ModuleUninitVariable(TypeData) 
instance VarB = ModuleUninitVariable(TypeData)
instance VarC = ModuleUninitVariable(TypeData)
instance VarCounter = ModuleVariable({0..M},0)

instance aClear = ModuleMutexSemaphore(TypeThreadID)
instance bClear = ModuleSignallingSemaphore(TypeThreadID)
instance cClear = ModuleSignallingSemaphore(TypeThreadID)
instance aSignal = ModuleSignallingSemaphore(TypeThreadID)
instance bSignal = ModuleSignallingSemaphore(TypeThreadID)
instance cSignal = ModuleSignallingSemaphore(TypeThreadID)

runWith(hide,p)=
  VarA::runWith(hide,
  VarB::runWith(hide,
  VarC::runWith(hide,
  VarCounter::runWith(hide,
  aClear::runWith(hide,
  bClear::runWith(hide,
  cClear::runWith(hide,
  aSignal::runWith(hide,
  bSignal::runWith(hide,
  cSignal::runWith(hide,
    p
  ))))))))))

SyncA(me,avalue) =
  Call!me!ASync!avalue ->
  --aClear.down
  aClear::downChan!me ->
  --a = me
  VarA::setValue!avalue ->
  --bClear.up
  bClear::upChan!me ->
  --aSignal.down
  aSignal::downChan!me ->
  --val result = (b,c,counter)
  VarB::getValue?b ->
  VarC::getValue?c ->
  VarCounter::getValue?counter ->
  --bSignal.up
  bSignal::upChan!me ->
  --result ->
  Return!me!ASync!b!c!counter ->
  SKIP

SyncB(me,bvalue) =
  Call!me!BSync!bvalue ->
  --bClear.down
  bClear::downChan!me ->
  --b=me
  VarB::setValue!bvalue ->
  --cClear.up
  cClear::upChan!me ->
  --bSignal.down
  bSignal::downChan!me ->
  --val result = (a,c,counter)
  VarA::getValue?a ->
  VarC::getValue?c ->
  VarCounter::getValue?counter ->
  --cSignal.up
  cSignal::upChan!me ->
  --result
  Return!me!BSync!a!c!counter ->
  SKIP

SyncC(me,cvalue) =
  Call!me!CSync!cvalue ->
  --cClear.down
  cClear::downChan!me ->
  --c = me
  VarC::setValue!cvalue ->
  --aSignal.up
  aSignal::upChan!me ->
  --cSignal.down
  cSignal::downChan!me ->
  --val result = (a,b,counter)
  VarA::getValue?a ->
  VarB::getValue?b ->
  VarCounter::getValue?counter ->
  --counter += 1
  VarCounter::setValue!((counter+1)%M) ->
  --aClear.up
  aClear::upChan!me ->
  --result
  Return!me!CSync!a!b!counter ->
  SKIP
