include "common.csp"

exchange(me,datasend) = Monitor::synchronized(me,
  --Start event for Linearizer
  Call.me.datasend->
  --while(step == 2) wait()
  Monitor::whileWait(me, \ktrue,kfalse @
    Step::getValue?step ->
    if step==2 then ktrue else kfalse
  );
  --if(step == 0)
  Step::getValue?step -> if step==0 then (
    --data = x;
    Data::setValue!datasend -> 
    --step = 1;
    Step::setValue!1 ->
    --while(step == 1) wait()
    Monitor::whileWait(me, \ktrue,kfalse @
      Step::getValue?step ->
      if step==1 then ktrue else kfalse
    );
    --assert(step == 2)
    Step::getValue?step ->
    if step!=2 then STOP else
    --step = 0
    Step::setValue!0 ->
    --notifyAll()
    Monitor::notifyAll(me);
    --data
    Data::getValue?res ->
    --return
    Return.me.res -> 
    SKIP
  )else(
    --val result = data;
    Data::getValue?res -> (
    --data = x;
    Data::setValue!datasend ->
    --step = 2; 
    Step::setValue!2 ->
    --notifyAll()
    Monitor::notifyAll(me);
    --result
    Return.me.res ->
    SKIP
  ))
)