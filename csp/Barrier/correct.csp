include "common.csp"


instance VarCount = VariableMin({0..N},0)
instance VarLeaving = VariableMin(Bool,False)
instance Monitor = ModuleMonitor(TypeThreadID)

runWith(hide,p)=
  VarCount::runWith(hide,
  VarLeaving::runWith(hide,
  Monitor::runWith(hide,hide,
    p
  )))

sync(me)=
  Call!me ->
  --synchronized
  Monitor::enter(me);
    --while(leaving) wait()
    Monitor::whileWait(me, \ktrue,kfalse @
      VarLeaving::getValue?leaving ->
      if leaving then ktrue else kfalse
    );
    --count+=1
    VarCount::getValue?x ->
    if x==N then div else
    VarCount::setValue!(x+1) -> (
    --if(count==n)
    if x+1==N then (
      --leaving=true
      VarLeaving::setValue!True ->
      --count-=1
      VarCount::setValue!x ->
      --notifyAll()
      Monitor::notifyAll(me)
    ) else (
      --while(!leaving) wait()
      Monitor::whileWait(me, \ktrue,kfalse @
        VarLeaving::getValue?leaving ->
        if (not leaving) then ktrue else kfalse
      );
      --count-=1
      VarCount::getValue?y -> 
      if y==0 then div else
      VarCount::setValue!y-1 ->
      --if(count==0)
      if(y-1==0) then (
        --leaving=false
        VarLeaving::setValue!False ->
        --notifyAll()
        Monitor::notifyAll(me)
      ) else SKIP
    )
  );
  Monitor::exit(me);
  Return!me ->
  SKIP



