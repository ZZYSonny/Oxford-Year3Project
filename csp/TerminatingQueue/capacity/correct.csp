include "common.csp"

instance VarQueue=VariableMin(TypeQueue,<>)
instance VarWaiting=VariableMin({0..NThread},0)
instance VarDone=VariableMin(Bool,false)
instance Monitor=ModuleMonitor(TypeThread)

enqueue(me,x)=
  Call!me!(EnqueueCall.x) ->
  Monitor::synchronized(me,
    --if(!done)
    VarDone::getValue?done ->
    if (not done) then (
      --queue.enqueue(x), block until the queue is not full
      VarQueue::getValue?q:diff(TypeQueue, Arrangement(NQueueCapacity,TypeData)) ->
      VarQueue::setValue!(q^<x>) ->
      --if(waiting > 0) notify()
      VarWaiting::getValue?waiting ->
      if(waiting>0) then Monitor::notify(me) else SKIP
    ) else SKIP
  );
  Return!me!(EnqueueReturn) ->
  SKIP
  
dequeue(me)=
  Call!me!(DequeueCall) ->
  Monitor::enter(me);
  --if(!done && queue.isEmpty)
  VarDone::getValue?done ->
  VarQueue::getValue?q ->
  (if(not done and q==<>) then (
    --if(waiting == numWorkers-1)
    VarWaiting::getValue?x->
    if(x==NThread-1) then(
      --done=true
      VarDone::setValue!true->
      --notifyAll
      Monitor::notifyAll(me)
    )else(
      --waiting+=1
      if(x==NThread) then div else
      VarWaiting::setValue!(x+1) ->
      --while(queue.isEmpty && !done) wait()
      Monitor::whileWait(me, \ktrue,kfalse @
        VarQueue::getValue?q ->
        VarDone::getValue?done ->
        if(q==<> and not done) then ktrue else kfalse
      );
      --waiting -= 1
      VarWaiting::getValue?y->
      if (y==0) then div else
      VarWaiting::setValue!(y-1)->
      SKIP
    )
  )else SKIP);
  --if(done) None else Some(queue.dequeue)
  VarDone::getValue?done ->
  if (done) then (
    Monitor::exit(me);
    Return!me!(DequeueReturn.None) ->
    --the thread should stop any work
    STOP
  ) else (
    VarQueue::getValue?q ->
    if(q==<>) then SKIP else
    VarQueue::setValue!(tail(q)) ->
    Monitor::exit(me);
    Return!me!(DequeueReturn.(Some.head(q))) ->
    SKIP
)

runWith(hide,hideSpurious,P)=
  VarQueue::runWith(hide,
  VarWaiting::runWith(hide,
  VarDone::runWith(hide,
  Monitor::runWith(hide,hideSpurious,
    P
  ))))