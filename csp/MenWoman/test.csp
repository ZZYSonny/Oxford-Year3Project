include "correct.csp"

hide = True
channel Sync: TypeThreadID.TypeThreadID.TypeThreadID.TypeThreadID
--(Identity of thread calling ManSync).(Return of ManSync).(Identity of thread calling WomanSync).(Return of WomanSync)

Spec = 
  Sync?man:TypeMan?woman:TypeWoman!woman!man ->
  Spec

--Linearizer for a process
Lin(All,me)= (
  Call!me!ManSync->
  Sync!me?mereturn?other?otherreturn ->
  Return!me!ManSync!mereturn ->
  Lin(All,me)
)[](
  Call!me!WomanSync ->
  Sync?other?otherreturn!me?mereturn ->
  Return!me!WomanSync!mereturn ->
  Lin(All,me)
)
LinEvents(All,me)=union({
  ev | ev<-{|Sync|},
  let Sync.t1.a.t2.b=ev within
    countEqualList(me,<t1,t2>)==1
},{|Call.me,Return.me|})
Linearizers(All)= || me: All @ [LinEvents(All,me)]  Lin(All,me)

System1 = runWith(hide,
    repeat(manSync(Man1)) |||
    repeat(womanSync(Woman1))
)
Spec1 = (Linearizers({Man1,Woman1}) [|{|Sync|}|] Spec) \ {|Sync|}
assert Spec1 [T= System1
--assert System1 :[deadlock free]

System2 = runWith(hide,
    repeat(manSync(Man1)) |||
    repeat(manSync(Man2)) |||
    repeat(womanSync(Woman2)) |||
    repeat(womanSync(Woman1))
)
Spec2 = (Linearizers({Man1,Man2,Woman1,Woman2}) [|{|Sync|}|] Spec) \ {|Sync|}
assert Spec2 [T= System2
